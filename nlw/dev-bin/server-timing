#!/usr/bin/env perl
# @COPYRIGHT@
use strict;
use warnings;
use FindBin;
use lib "$FindBin::Bin/../lib";
use Socialtext::Timer;
use Socialtext::System qw/shell_run/;
$Socialtext::System::SILENT_RUN = 1;
use Sys::Hostname qw/hostname/;
use Term::ANSIColor;
use Getopt::Long;

my $workspace;
my $server       = "http://" . hostname() . ':' . ($< + 20000);
my $username     = 'devnull1@socialtext.com';
my $password     = 'd3vnu11l';
my $num_requests = 10;
my $concurrency  = 1;
my $dev_env      = 0;
my $testnum;
GetOptions(
    'workspace=s'   => \$workspace,
    'server=s'      => \$server,
    'username=s'    => \$username,
    'password=s'    => \$password,
    'num=s'         => \$num_requests,
    'concurrency=s' => \$concurrency,
    'testnum=s'     => \$testnum,
    'dev-env'       => \$dev_env,
) or usage();
usage('Workspace is mandatory') unless $workspace;

my @urls = (
    {
        name => 'REST workspace pages',
        path => q{/data/workspaces/:ws/pages},
    },
    {
        name => 'NLW All pages',
        path => q{/:ws/index.cgi?action=changes&changes=all},
    },
    {
        name => 'Feed Recent Changes',
        path => q{/feed/workspace/:ws?category=Recent%20Changes&count=100},
    },
    {
        name => 'Basic Search',
        path => q{/:ws/index.cgi?search_term=the&action=search},
    },
);
if (defined $testnum) {
    die "testnum should be between 0 and $#urls!\n" if $testnum >= @urls;
    @urls = ($urls[$testnum]);
}

for my $url (@urls) {
    $url->{path} =~ s/:ws/$workspace/g;
    my $start_time = time;
    shell_run("ab -A $username:$password -c $concurrency -n $num_requests "
        . "'$server$url->{path}'");
    my $end_time = time;
    summarize($start_time, $end_time);
}

exit;

sub summarize {
    my $start_time = shift;
    my $end_time   = shift;
    my $toolname   = "st-appliance-summarize-perf";
    my $summarizer = "/usr/sbin/$toolname";
    if ($dev_env) {
        $summarizer = "$^X -I$ENV{ST_CURRENT}/nlw/lib"
            . " -I$ENV{ST_CURRENT}/appliance/libsocialtext-appliance-perl/lib"
            . " $ENV{ST_CURRENT}/appliance/libsocialtext-appliance-perl/sbin/$toolname";
    }
    die "Can't find $summarizer" unless $dev_env or -x $summarizer;
    shell_run("$summarizer --start-time $start_time --end-time $end_time");
}

sub usage {
    my $msg = shift || '';
    die <<EOT;
$msg

USAGE: $0 <options>
Where options are:
  --server <server>     server to test
  --workspace <name>    workspace to test
  --username <user>     http auth username
  --password <pass>     http auth password

  --num  <num>          Number of requests to make
  --concurrency <num>   Concurrent requests
EOT
}
